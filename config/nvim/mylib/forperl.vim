augroup filetypedetect
autocmd! BufNewFile,BufRead *.t setf perl
autocmd! BufNewFile,BufRead *.psgi setf perl
autocmd! BufNewFile,BufRead *.tt setf tt2html
augroup END
autocmd BufNewFile *.pl 0r $HOME/.config/nvim/template/perl-script.txt
autocmd BufNewFile *.t  0r $HOME/.config/nvim/template/perl-test.txt
autocmd BufNewFile *.pm  0r $HOME/.config/nvim/template/perl-module.txt
"set pm_template;
function! s:pm_template()
    let path = substitute(expand('%'), '.*lib/', '', 'g')
    let path = substitute(path, '[\\/]', '::', 'g')
    let path = substitute(path, '\.pm$', '', 'g')

    call append(0, 'package ' . path . ';') "set package name as file name
    call append(1, 'use strict;')
    call append(2, 'use warnings;')
    call append(3, 'use utf8;')
    call append(4, '')
    call append(5, '')
    call append(6, '')
    call append(7, '1;')
    call cursor(6, 0)
    " echomsg path
endfunction
"detect package name miss
function! s:get_package_name()
    let mx = '^\s*package\s\+\([^ ;]\+\)'
    for line in getline(1, 5)
        if line =~ mx
        return substitute(matchstr(line, mx), mx, '\1', '')
        endif
    endfor
    return ""
endfunction

function! s:check_package_name()
    let path = substitute(expand('%:p'), '\\', '/', 'g')
    let name = substitute(s:get_package_name(), '::', '/', 'g') . '.pm'
    if path[-len(name):] != name
        echohl WarningMsg
        echomsg "warning: package name and path name are different"
        echomsg "please fix it."
        echohl None
    endif
endfunction

au! BufWritePost *.pm call s:check_package_name()
autocmd BufNewFile *.pm call s:pm_template()
